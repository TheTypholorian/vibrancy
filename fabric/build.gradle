plugins {
    id 'multiloader-loader'
    id 'fabric-loom'
}

ext.minecraft_version = "1.21.1"

repositories {
    maven {
        name = 'Modrinth'
        url = "https://api.modrinth.com/maven"
    }
}
dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.layered {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${parchment_minecraft}:${parchment_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    include(modImplementation("foundry.veil:veil-fabric-${minecraft_version}:${veil_version}") {
        exclude group: "maven.modrinth"
    })
    implementation("maven.modrinth:sodium:${sodium_version}-neoforge")
}

loom {
    def aw = project(':common').file("src/main/resources/${mod_id}.accesswidener")
    if (aw.exists()) {
        accessWidenerPath.set(aw)
    }
    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }
    runs {
        client {
            client()
            setConfigName('Fabric Client')
            ideConfigGenerated(true)
            runDir('runs/client')
        }
        server {
            server()
            setConfigName('Fabric Server')
            ideConfigGenerated(true)
            runDir('runs/server')
        }
    }
}